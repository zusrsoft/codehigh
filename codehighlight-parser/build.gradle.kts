import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidLibrary)
    alias(libs.plugins.mavenPublish)
}

kotlin {
    jvmToolchain(21)

    android {
        namespace = "com.hrm.codehigh.parser"
        compileSdk = libs.versions.android.compileSdk.get().toInt()
        minSdk = libs.versions.android.minSdk.get().toInt()

        withJava()
        withHostTestBuilder {}.configure {}
        withDeviceTestBuilder {
            sourceSetTreeName = "test"
        }

        compilerOptions {}

        // 将 consumer-rules.pro 打包到 AAR，下游开启 R8 时自动生效
        // 仅声明类元数据属性依赖，不锁定任何具体类/成员，对下游 R8 优化零影响
        optimization {
            consumerKeepRules.publish = true
            consumerKeepRules.files.add(project.file("consumer-rules.pro"))
        }
    }

    listOf(
        iosArm64(),
        iosSimulatorArm64()
    ).forEach { iosTarget ->
        iosTarget.binaries.framework {
            baseName = "CodeHighlightParser"
            isStatic = true
        }
    }

    jvm()

    js {
        browser()
        binaries.executable()
    }

    @OptIn(ExperimentalWasmDsl::class)
    wasmJs {
        browser()
        binaries.executable()
    }

    sourceSets {
        commonTest.dependencies {
            implementation(libs.kotlin.test)
        }
    }
}

mavenPublishing {
    publishToMavenCentral(true)

    signAllPublications()

    coordinates("io.github.zusrsoft", "codehighlight-parser", rootProject.property("VERSION").toString())

    pom {
        name.set("CodeHigh Parser")
        description.set(
            """
            Cross-platform code tokenization and incremental parsing layer for CodeHigh.
            """.trimIndent()
        )
        inceptionYear.set("2026")
        url.set("https://github.com/zusrsoft/codehigh")
        licenses {
            license {
                name.set("MIT License")
                url.set("https://opensource.org/licenses/MIT")
                distribution.set("repo")
            }
        }
        developers {
            developer {
                id.set("zusrsoft")
                name.set("zusrsoft")
                url.set("https://github.com/zusrsoft/")
            }
        }
        scm {
            url.set("https://github.com/zusrsoft/codehigh")
            connection.set("scm:git:git://github.com/zusrsoft/codehigh.git")
            developerConnection.set("scm:git:ssh://git@github.com/zusrsoft/codehigh.git")
        }
    }
}
